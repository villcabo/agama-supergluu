Flow org.gluu.agama.supergluu.main
     Basepath ""
     Configs conf
Log "@debug --> Main flow started"

authService = Call io.jans.as.server.service.AuthenticationService#class
cdiUtil = Call io.jans.service.cdi.util.CdiUtil#bean authService
authResult = {}
obj = { success: true, showError: false, errorTitle: "", errorMessage: "" }

Repeat conf.maxLoginAttempt times max
    mainForm = RRF "main.ftlh" obj
    Log "@debug Main form result %" mainForm

    authResult.success = Call cdiUtil authenticate mainForm.username mainForm.password
    authResult.uid = mainForm.username
    When authResult.success is true
        userData = Call org.gluu.agama.supergluu.IdentityProcessor#accountFromUid authResult.uid
        Log "@debug Call accountFromUid response %" userData
        uid = userData.uid
        listFlow = Trigger org.gluu.agama.supergluu.list userData conf
        Log "@debug List flow completed: %" listFlow
        When listFlow.success is true
            Finish uid
    Otherwise
        obj.showError = true
        obj.errorTitle = "Invalid credentials"
        obj.errorMessage = "Wrong user name or password"

res = { success: false, error: "Login attempt exceeded." }
Finish res
