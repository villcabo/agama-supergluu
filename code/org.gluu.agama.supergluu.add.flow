Flow org.gluu.agama.supergluu.add
     Basepath ""
     Inputs userData
Log "@debug --> Add a passkey flow started! <--"

inum = userData.inum
name = userData.name
fidoEnroller = Call org.gluu.agama.supergluu.enroll.FidoEnroller#new
obj = { attestation: "{}", isAttestation: false, showError: false, errorTitle: "", errorMessage: "" }

Repeat 50 times max
    addDeviceForm = RRF "passkey-add.ftlh" obj
    Log "@debug Add form response %" addDeviceForm

    When addDeviceForm.addPasskeyBtn is ""
        detestable = Call fidoEnroller getAttestationMessage inum
        obj.isAttestation = true
        obj.attestation = detestable
        obj.showError = false

    When addDeviceForm.skipped is "skipped"
        obj.isAttestation = false
        obj.showError = true
        obj.errorTitle = "Passkey registration failed."
        obj.errorMessage = addDeviceForm.errorMessage

    When addDeviceForm.tokenResponse is not ""
        key | E = Call fidoEnroller verifyRegistration inum addDeviceForm.tokenResponse
        When E is not null
            Log "@error FidoEnroller.verifyRegistration is crashed: %" E
            obj.isAttestation = false
            obj.showError = true
            obj.errorTitle = "Passkey registration failed."
            obj.errorMessage = E.message
        Otherwise
            Log "@debug FidoEnroller.verifyRegistration is success, key %" key
            nicknameTrigger = Trigger org.gluu.agama.supergluu.nickname key userData
            Log "@debug Nickname trigger response %" nicknameTrigger
            obj.isAttestation = false
            Finish nicknameTrigger

    When addDeviceForm.cancelBtn is ""
        res = { success: false, message: "Cancel event" }
        Finish res

res = { success: false, error: "Passkey registration attempt exceeded." }
Finish res