Flow org.gluu.agama.supergluu.list
     Basepath ""
     Inputs userData scimSetting
Log "@debug --> List flow started"

inum = userData.inum
scimFido2Helper = Call org.gluu.agama.supergluu.ScimFido2Helper#new scimSetting
fidoDevice = Call scimFido2Helper getFidoDeviceByUser inum
obj = { fidoDevice: fidoDevice, showError: false, errorTitle: "", errorMessage: "", canEdit: false }

Repeat scimSetting.maxLoginAttempt times max
    listForm = RRF "list.ftlh" obj
    Log "@debug List form response %" listForm

    When listForm.addBtn is ""
        addFlow = Trigger org.gluu.agama.supergluu.add userData scimSetting
        Log "@debug Enrollment flow %" addFlow
        obj.showError = false
        When addFlow.success is true
            obj.fidoDevice = Call scimFido2Helper getFidoDeviceByUser inum

    When listForm.loginBtn is ""
        obj.showError = false
        authFlow = Trigger org.gluu.agama.supergluu.authn userData scimSetting
        Log "@debug Authenticator flow %" authFlow
        When authFlow.success is false
            obj.showError = true
            obj.errorMessage = authFlow.message
        Otherwise
            res = { success: true, message: "Authenticator is success" }
            Finish res

    When listForm.editItemBtn is ""
        obj.canEdit = true
        obj.showError = false

    When listForm.cancelItemBtn is ""
        obj.canEdit = false
        obj.showError = false

    When listForm.deviceId is not null and listForm.deviceId is not ""
        obj.canEdit = false
        obj.showError = false
        Log "@debug Attempting to update device -> inum: %, deviceId: %, nickName: %" inum listForm.deviceId listForm.nickname
        nickNamed | E = Call scimFido2Helper updateDevice inum listForm.deviceId listForm.nickname
        When E is not null
            Log "@debug Failed to set a nickname for % device, Error %" userData.uid E
            obj.showError = true
            obj.errorTitle = "Failed to set a new nickname"
            obj.errorMessage = E.message
        Otherwise
            Log "@debug Device % updated %" listForm.deviceId nickNamed
            obj.fidoDevice = Call scimFido2Helper getFidoDeviceByUser inum

res = { success: false, message: "SuperGluu list limited exceeded" }
Finish res
