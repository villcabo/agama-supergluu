Flow org.gluu.agama.supergluu.list
     Basepath ""
     Inputs userData scimSetting
Log "@debug --> List passkey flow started! <--"

inum = userData.inum
uid = userData.uid
fidoValidator = Call org.gluu.agama.supergluu.authn.FidoValidator#new
scimFido2Helper = Call org.gluu.agama.supergluu.ScimFido2Helper#new scimSetting
fidoDevice = Call scimFido2Helper getFidoDeviceByUser inum
obj = { fidoDevice: fidoDevice, assertion: "{}", isAssertion: false, showError: false, errorTitle: "", errorMessage: "", canEdit: false }

Repeat 50 times max
    listForm = RRF "passkey-list.ftlh" obj
    Log "@debug List passkey form response %" listForm

    When listForm.addAPasskeyBtn is ""
        addTrigger = Trigger org.gluu.agama.supergluu.add userData
        Log "@debug Add passkey trigger response %" addTrigger
        obj.showError = false
        obj.isAssertion = false
        When addTrigger.success is true
            obj.fidoDevice = Call scimFido2Helper getFidoDeviceByUser inum

    When listForm.loginWithPasskeyBtn is ""
        desAssertion = Call fidoValidator assertionRequest uid
        obj.showError = false
        obj.isAssertion = true
        obj.assertion = desAssertion

    When listForm.skipped is "skipped"
        obj.isAssertion = false
        obj.showError = true
        obj.errorTitle = "Passkey authentication failed."
        obj.errorMessage = listForm.errorMessage

    When listForm.editItemBtn is ""
        obj.canEdit = true
        obj.isAssertion = false
        obj.showError = false

    When listForm.cancelItemBtn is ""
        obj.canEdit = false
        obj.isAssertion = false
        obj.showError = false

    When listForm.deviceId is not ""
        obj.canEdit = false
        obj.isAssertion = false
        obj.showError = false
        nickNamed | E = Call scimFido2Helper updateDevice inum listForm.deviceId listForm.nickname
        When E is not null
            Log "@debug Failed to set a nickname for % device" uid
            obj.showError = true
            obj.errorTitle = "Passkey failed to set a new nickname"
            obj.errorMessage = E.message
        Otherwise
            Log "@debug Device % updated %" listForm.deviceId nickNamed
            obj.fidoDevice = Call scimFido2Helper getFidoDeviceByUser inum

    When listForm.tokenResponse is not ""
        When listForm.tokenResponse is not null
            verifyResponse | E = Call fidoValidator verify listForm.tokenResponse
            When E is not null
                Log "@debug FidoValidator.verify is crashed: %" E
                obj.showError = true
                obj.errorTitle = "Passkey authentication failed."
                obj.errorMessage = E.message
            Otherwise
                Log "@debug FidoValidator.verify is success: %" verifyResponse
                res = { success: true, message: "Passkey authentication success" }
                Finish res

res = { success: false, message: "Passkey list limited exceeded" }
Finish res
